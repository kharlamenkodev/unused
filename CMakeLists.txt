cmake_minimum_required(VERSION 3.1...3.14)

project(unused VERSION 0.0.1 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 17)

set(UNUSED_TARGET_NAME ${PROJECT_NAME})

include(GNUInstallDirs)

set(UNUSED_INCLUDE_DIR "${PROJECT_SOURCE_DIR}/include/")
set(UNUSED_CONFIG_INSTALL_DIR "${CMAKE_INSTALL_DATADIR}/cmake/${PROJECT_NAME}/" CACHE INTERNAL "")
set(UNUSED_INCLUDE_INSTALL_DIR "${CMAKE_INSTALL_INCLUDEDIR}/")
set(UNUSED_TARGETS_EXPORT_NAME "${PROJECT_NAME}Targets")
set(UNUSED_CMAKE_CONFIG_DIR "${PROJECT_SOURCE_DIR}/cmake")
set(UNUSED_CMAKE_PROJECT_CONFIG_FILE "${UNUSED_CMAKE_CONFIG_DIR}/${PROJECT_NAME}Config.cmake")
set(UNUSED_CMAKE_PROJECT_TARGETS_FILE "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Targets.cmake")


add_library(${UNUSED_TARGET_NAME} INTERFACE)
target_include_directories(${UNUSED_TARGET_NAME} INTERFACE
    $<BUILD_INTERFACE:${UNUSED_INCLUDE_DIR}>
    $<INSTALL_INTERFACE:${UNUSED_INCLUDE_INSTALL_DIR}>
)

install(
    DIRECTORY ${UNUSED_INCLUDE_DIR}
    DESTINATION ${UNUSED_INCLUDE_INSTALL_DIR}
)

install(FILES ${UNUSED_CMAKE_PROJECT_CONFIG_FILE}
    DESTINATION ${UNUSED_CONFIG_INSTALL_DIR})

export(
    TARGETS ${UNUSED_TARGET_NAME}
    NAMESPACE ${PROJECT_NAME}::
    FILE ${UNUSED_CMAKE_PROJECT_TARGETS_FILE}
)

install(
    TARGETS ${UNUSED_TARGET_NAME}
    EXPORT ${UNUSED_TARGETS_EXPORT_NAME}
    INCLUDES DESTINATION ${UNUSED_INCLUDE_INSTALL_DIR}
)
install(
    EXPORT ${UNUSED_TARGETS_EXPORT_NAME}
    NAMESPACE ${PROJECT_NAME}::
    DESTINATION ${UNUSED_CONFIG_INSTALL_DIR}
)